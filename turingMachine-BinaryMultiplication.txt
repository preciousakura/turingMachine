init: I
accept: I

// ATUALIZAÇÃO DIA 18/09/2020
// LEGENDA DAS VARIÁVEIS
// E = 0
// X = 0
// Z = 0
// A = 0
// D = 1
// Y = 1
// U = 1
// B = 1

// TESTES E NÚMEROS DE PASSOS

// 11*10 = 110 (64 PASSOS)
// 101*1100 =  111100 (205 PASSOS)
// 1010*000 = 0 (20 PASSOS)
// 000*1010 = 0 (20 PASSOS)
// 11011110*101 = 10001010110 (496 PASSOS)
// 111010*1101011 =  1100000111110 (1034 PASSOS)




I,1
I,1,<

I,0
I,0,<

I,_
verifica_se_tem_so_0,-,>

verifica_se_tem_so_0,0
verifica_se_tem_so_0,0,>

verifica_se_tem_so_0,*
deu_zero,_,>

deu_zero,1
deu_zero,_,>

deu_zero,0
deu_zero,_,>

deu_zero,_
deu_zero_esquerda,_,<

deu_zero_esquerda,_
deu_zero_esquerda,_,<

deu_zero_esquerda,0
deu_zero_esquerda,_,<

deu_zero_esquerda,*
deu_zero_esquerda,_,<

deu_zero_esquerda,1
deu_zero_esquerda,_,<

deu_zero_esquerda,-
FIM,0,-

verifica_se_tem_so_0,1
ir_ate_final,1,>

ir_ate_final,1
ir_ate_final,1,>

ir_ate_final,0
ir_ate_final,0,>

ir_ate_final,u
ir_ate_final,u,>

ir_ate_final,z
ir_ate_final,z,>

ir_ate_final,x
ir_ate_final,x,>

ir_ate_final,y
ir_ate_final,y,>

ir_ate_final,*
so_zero,*,>

so_zero,0
so_zero,0,>

so_zero,_
deu_zero_esquerda,_,<

so_zero,1
ir_ate_final,1,>

ir_ate_final,_
comecar_multiplicar,_,<

// SE O PRIMEIRO NÚMERO FOR 0 VAI IGNORAR E VAI PRO PROXIMO NUMERO

comecar_multiplicar,0
nova_multiplicacao,z,<

// SE O PRIMEIRO NÚMERO FOR 1

comecar_multiplicar,1
achar_asterisco_1,u,<

achar_asterisco_1,1
achar_asterisco_1,1,<

achar_asterisco_1,0
achar_asterisco_1,0,<

achar_asterisco_1,*
colocar_1,*,<

colocar_1,1
imprimi_1,y,<

colocar_1,0
imprimi_0,x,<

imprimi_1,1
imprimi_1,1,<

imprimi_1,0
imprimi_1,0,<

imprimi_1,-
imprimi_1,-,<

imprimi_1,_
achar_outro_numero,1,>

imprimi_0,1
imprimi_0,1,<

imprimi_0,0
imprimi_0,0,<

imprimi_0,-
imprimi_0,-,<

imprimi_0,_
achar_outro_numero,0,>

achar_outro_numero,-
achar_outro_numero,-,>

achar_outro_numero,1
achar_outro_numero,1,>

achar_outro_numero,0
achar_outro_numero,0,>

achar_outro_numero,y
colocar_1,y,<

achar_outro_numero,x
colocar_1,x,<

colocar_1,-
voltar_numeros,-,>

voltar_numeros,y
voltar_numeros,1,>

voltar_numeros,x
voltar_numeros,0,>

voltar_numeros,*
proxima_multi,*,>

// proximo valor a ser multiplicado
proxima_multi,0
proxima_multi,0,>

proxima_multi,1
proxima_multi,1,>

proxima_multi,-
proxima_multi,-,>

proxima_multi,*
proxima_multi,*,>

proxima_multi,E
proxima_multi,E,> 

proxima_multi,D
proxima_multi,D,>

proxima_multi,z
nova_multiplicacao,z,<

proxima_multi,u
nova_multiplicacao,u,<

// se o valor a ser multiplicado for 1

nova_multiplicacao,1
zero_a_esquerda,u,<

zero_a_esquerda,*
zero_a_esquerda,*,<

zero_a_esquerda,0
zero_a_esquerda,0,<

zero_a_esquerda,1
zero_a_esquerda,1,<

zero_a_esquerda,-
coloca_zero_a_esquerda,-,<

coloca_zero_a_esquerda,0
achar_o_vezes,E,>

coloca_zero_a_esquerda,E
coloca_zero_a_esquerda,E,<

coloca_zero_a_esquerda,_
achar_o_vezes,E,>

coloca_zero_a_esquerda,D
coloca_zero_a_esquerda,D,<

coloca_zero_a_esquerda,1
achar_o_vezes,D,>

achar_o_vezes,-
achar_o_vezes,-,>

achar_o_vezes,1
achar_o_vezes,1,>

achar_o_vezes,E
achar_o_vezes,E,>

achar_o_vezes,D
achar_o_vezes,D,>

achar_o_vezes,0
achar_o_vezes,0,>

achar_o_vezes,*
hora_de_somar,*,<

hora_de_somar,0
acha_traco_para_somar,x,<

acha_traco_para_somar,1
acha_traco_para_somar,1,<

acha_traco_para_somar,0
acha_traco_para_somar,0,<

acha_traco_para_somar,x
acha_traco_para_somar,x,<

acha_traco_para_somar,y
acha_traco_para_somar,y,<

acha_traco_para_somar,-
somar,-,<

somar,E
somar,E,<

somar,D
somar,D,<

somar,B
somar,B,<

somar,A
somar,A,<

somar,_
achar_novo_num_para_somar,A,>

somar,1
achar_novo_num_para_somar,B,>

somar,0
achar_novo_num_para_somar,A,>

achar_novo_num_para_somar,E
achar_novo_num_para_somar,E,>

achar_novo_num_para_somar,D
achar_novo_num_para_somar,D,>

achar_novo_num_para_somar,B
achar_novo_num_para_somar,B,>

achar_novo_num_para_somar,A
achar_novo_num_para_somar,A,>

achar_novo_num_para_somar,-
achar_novo_num_para_somar,-,>

achar_novo_num_para_somar,1
achar_novo_num_para_somar,1,>

achar_novo_num_para_somar,0
achar_novo_num_para_somar,0,>

achar_novo_num_para_somar,x
hora_de_somar,x,<

achar_novo_num_para_somar,y
hora_de_somar,y,<

hora_de_somar,1
somar_por_1_1,y,<

hora_de_somar,-
renomear_soma,-,<

renomear_soma,E
renomear_soma,E,<

renomear_soma,D
renomear_soma,D,<

renomear_soma,A
renomear_soma,0,<

renomear_soma,B
renomear_soma,1,<

renomear_soma,1
renomear_soma,1,<

renomear_soma,0
renomear_soma,0,<

renomear_soma,_
achar_tracu,_,>

achar_tracu,1
achar_tracu,1,>

achar_tracu,0
achar_tracu,0,>

achar_tracu,E
achar_tracu,E,>

achar_tracu,D
achar_tracu,D,>

achar_tracu,-
renomea,-,>

renomea,y
renomea,1,>

renomea,x
renomea,0,>

renomea,*
proxima_multi,*,>

somar_por_1_1,1
somar_por_1_1,1,<

somar_por_1_1,0
somar_por_1_1,0,<

somar_por_1_1,-
soma_por_1,-,<

soma_por_1,E
soma_por_1,E,<

soma_por_1,D
soma_por_1,D,<

soma_por_1,A
soma_por_1,A,<

soma_por_1,B
soma_por_1,B,<

soma_por_1,1
vai_um,A,<

vai_um,1
vai_um,0,<

vai_um,_
achar_novo_num_para_somar,1,>

vai_um,0
achar_novo_num_para_somar,1,>

soma_por_1,0
achar_novo_num_para_somar,B,>

soma_por_1,_
achar_novo_num_para_somar,B,>

// se o valor a ser multiplicado for 0

nova_multiplicacao,0
zero_a_esquerda_0,z,<

zero_a_esquerda_0,*
zero_a_esquerda_0,*,<

zero_a_esquerda_0,0
zero_a_esquerda_0,0,<

zero_a_esquerda_0,1
zero_a_esquerda_0,1,<


zero_a_esquerda_0,-
coloca_zero_a_esquerda_0,-,<

coloca_zero_a_esquerda_0,E
coloca_zero_a_esquerda_0,E,<

coloca_zero_a_esquerda_0,D
coloca_zero_a_esquerda_0,D,<

coloca_zero_a_esquerda_0,1
proxima_multi,D,>

coloca_zero_a_esquerda_0,0
proxima_multi,E,>

coloca_zero_a_esquerda_0,_
proxima_multi,E,>

coloca_zero_a_esquerda_0,0
proxima_multi,E,>


// acabou a multiplicação, apagar tudo

nova_multiplicacao,*
apagar_tudo_direita,_,>

apagar_tudo_direita,u
apagar_tudo_direita,_,>

apagar_tudo_direita,z
apagar_tudo_direita,_,>


apagar_tudo_direita,_
apagar_tudo_esquerda,_,<

apagar_tudo_esquerda,_
apagar_tudo_esquerda,_,<

apagar_tudo_esquerda,1
apagar_tudo_esquerda,_,<

apagar_tudo_esquerda,0
apagar_tudo_esquerda,_,<

apagar_tudo_esquerda,-
ir_final_pra_acabar,_,<

ir_final_pra_acabar,E
ir_final_pra_acabar,0,<

ir_final_pra_acabar,D
ir_final_pra_acabar,1,<

ir_final_pra_acabar,0
ir_final_pra_acabar,0,<

ir_final_pra_acabar,1
ir_final_pra_acabar,1,<

ir_final_pra_acabar,_
FIM,_,>


accept: FIM





